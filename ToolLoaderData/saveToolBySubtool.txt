/**
	Manage save tool as *.ztl file
	
	MAIN SUBTOOL is 1st subtool when file is saved
	
	MAIN SUBTOOLL:
		• Has name as ztl file
		• Hold path to ztl file
	
	
	
  
  */
[RoutineDef, saveToolBySubtool
,
    [VarSet, path, "" ]	// path to .ztl file
    [VarSet, index, -1]	// index of main subtool
    [VarSet, nameFound, 0] 	// 
	[VarSet, incrementFound, 0]
	[VarSet, subtoolSctiveIndex, [SubToolGetActiveIndex]]
	
	//[Note, [StrLength, savePath ],, 0]

	[If, [StrLength, savePath ] > 0
	, // THEN
        [VarSet, path, savePath ]	// path to .ztl file
		
         [RoutineCall, searchForSubtoolByPath, path, index]

	, // ELSE
	
	    [RoutineCall, getSavePath, path, index]
	]

	
    [RoutineCall, moveSubtoolToTop, path, index]

	//[Note, [StrMerge, "[StrFind, DecimationMasterData, path ]: ", [StrFind, "DecimationMasterData", path ] ] ]

	/* SOME PLUGINS REDIRECT PATH to their own directories

		Lets put in back to track by laoding one of lates paths which tool name match current
	*/
	[If, [StrFind, "DecimationMasterData", path ] > -1 || [StrFind, "ZBRUSHMAXSYNC", path ] > -1,

		[VarSet, saveToolName, [FileNameExtract, path, 2 ]] // name of tool to save

		/* SEARCH SAME NAME IN LIST */
		[Loop, 3,

			[RoutineCall, loadToolPath, i ]  // load last tool path

			[VarSet, pathLoadedFileName, [FileNameExtract, TOOLPATHLOADED, 2 ]] // name of tool to save

			[If, ( nameFound == 0 && (([StrLength, saveToolName]==[StrLength, pathLoadedFileName]) && ([StrFind, saveToolName, pathLoadedFileName] > -1)))
			, // THEN


				//[Note, [StrMerge, "TOOLPATHLOADED: ", TOOLPATHLOADED ] ]

				//[Note, [StrMerge,
				//	"TOOLPATHLOADED: ", TOOLPATHLOADED, "\n\n",
				//	"path: ", path
				//]]

				[VarSet, nameFound, 1]

				[VarSet, path, TOOLPATHLOADED]
			]
		,i]

		//[Note, [StrMerge, "DECIAMTION NEW PATH: ", path ] ]

	]

	//[If, [Var, index ] < 0,  // DISABLE FOR DEV TESTING
		/* ASK TO SAVE TO LAST KNOWN LOCATION, IF CANCEL then RESET save path */
		[If, ! [MessageYesNo, [StrMerge, "SAVE TOOL ? ", path]],
			[VarSet, path, ""]]
	//]

	/* IF PATH EXIST */
	[If, [StrLength, path ] > 0,

		/* ADD file extension */ 
		[If, [StrFind, ".ztl", path ] == -1,
			[VarSet, path, [StrMerge, path, ".ztl"]]]

		[VarSet, pathNext, path ]

		/* ADD INCREMENT */ 
		[If, (#increment > 0), // THEN add increamanetal number

			/* SIMULATION OF FUCKIN WHILE LOOP -- loop then any increment file will be new */
			[Loop, 10,

				/* BLOCK IF NEXT FILE IS FOUND */
				[If, (#incrementFound == 0), // THEN add increamanetalnumber

					//[Note, [StrMerge,
					//	"i: ", i, "\n\n",
					//	"incrementFound: ", incrementFound
					//]]

					[VarSet, pathNext,  [FileNameAdvance, pathNext, 2 ] ]

					[If, ([FileExists, pathNext] == 1 ),

						[If, [MessageYesNo, [StrMerge, "FILE ", [FileNameExtract, pathNext, 2 ], " EXISTS GET NEXT INCREMENT ?         ", [FileNameAdvance, pathNext, 2 ] ]] == 0
						,// THEN USE THIS INCREMENT

							[VarSet, incrementFound, 1 ]

							/* CANCEL SAVE IF NOT OVERWRITE */
							[If, ! [MessageYesNo, [StrMerge, "OVERWRITE FILE ?                                                               ", pathNext]],
								[VarSet, pathNext, ""]]

						/* ELSE GET NEXT INCREMENT */
						]

					, // use first increment
						[VarSet, incrementFound, 1 ]
					]

					[If, [StrLength, pathNext ] > 0,
						[VarSet, path, pathNext]]
				]
			,i]
		]

		/* SAVE FILE IF PATH EXIST AND USER COMFIRM */
		[If, [StrLength, path ] > 0,

			[FileNameSetNext, path ]

			//[Note, [StrMerge, "SAVE PATH TEST: ", path ] ]
			[IPress, Tool:Save As]
			[RoutineCall, saveToolPath, 0, path ]

			//[VarDef, escapedPath, ""]
	
			[RoutineCall, escapePath, [FileNameExtract, path, 1 ]]
			
			
			[Note, [StrMerge, "TOOL SAVED\n\nDIR: ", escapedPath, "\n\nTOOL: ", [FileNameExtract, path, 2 ] ] ]
			//[Note, [StrMerge, "TOOL SAVED\n\nDIR: ", [FileNameExtract, path, 1 ], "\n\nTOOL: ", [FileNameExtract, path, 2 ] ] ]

		]
	]

	[SubToolSelect, subtoolSctiveIndex ]


, /* parameters */
	increment /* 0|1 */ , savePath
]


/*------------------------------------------------------------------------------
	
	1st SUBTOOLL:
		• Has name as ztl file
		• Hold path to ztl file
	
	
	Possible states of SELECTED SUBTOOL and FILENAME:
	
		• First subtool selected, names are same
		• First subtool selected, names are different
		• nth   subtool selected, names are different
		• nth   subtool selected, names are same
		• nth   subtool selected, subtool with tool name not exists

--------------------------------------------------------------------------------*/

[RoutineDef, getSavePath
,
	/* GET FILE PATH IF CONNECTION BETWEEN FILE AND SUBTOOL EXIST */
	[RoutineCall, searchForFilepathInSubTools, path, index]

	/* GET LAST TYPED FILE PATH IF CONNECTION BETWEEN FILE AND SUBTOOL EXIST */
	[If, [StrLength, path ] == 0,
         [RoutineCall, searchForSubtoolByPath, path, index]]

, /* parameters and output */
	path, index
]
/*------------------------------------------------------------------------------
	SEARCH FOR SUBTOOL WHICH HOLDS FILEPATH

	1) MAIN SUBTOOL CAN BE MOVED
	2) BUT MUST NOT BE RENAMED

	@return fiepath and subtool inde3x

--------------------------------------------------------------------------------*/
[RoutineDef, searchForFilepathInSubTools
,
	//[VarSet, xnamesubtool, ]
	//[VarSet, currentsubtoolpath, ]

	[VarSet, ismaintool,  0 ]

	[Loop, [SubToolGetCount],
		[If, [StrLength, path ] == 0
		,
			[SubToolSelect, i ]

			[VarSet, ismaintool,  0 ]

			[RoutineCall, isCurrentSubtoolNameSameAsFilename, ismaintool ]

			//[Note, [StrMerge, "Subtool: ", i,  "\n\nVAR: ismaintool", "\n\nVAL: ", ismaintool ] ]

			/* IF MAIN SUBTOOL FOUND */
			[If, (#ismaintool > 0), // THEN
				//[Note, "SET INDEX",, 0]
				[VarSet, path, [GetActiveToolPath]]

				[VarSet, index, i ]
			]
		]

	, i] // end loop

,
	/* parameters and output */
	path, index
]

/** moveSubtoolToTop
  */
[RoutineDef, moveSubtoolToTop
,
	/* IF PATH EXISTS */
	[If, [StrLength, path ] > 0,


		/* MOVE MAIN SUBTOOL TO TOP BEFORE SAVE */
		[If, [Var, index ] > 0,

			[VarSet, numbersubtool, (#index + 1) ]

			/* ASK FOR COMFIRM BEFORE MOVE */
            [If, [MessageYesNo, [StrMerge, "MAIN SUBTOOL '", [FileNameExtract, path, 2 ], "' is ", numbersubtool,  ". subtool in list.                                  MOVE THIS SUBTOOL TO TOP BEFORE SAVE ? " ]  ],

				[SubToolSelect, [Var, index] ]

				[IKeyPress,SHIFT,[IPress,Tool:SubTool:MoveUp]]
			]
		]
	]

, /* parameters and output */
	path, index
]



/*--------------------------------------------------------------------------------------------------------

	FIND SUBTOOL MATCHING NAME OF .ztl FILE

	Compare subtool name and subtool path,
	mian sutbtool returns PATH,
	but other subtools Returns its name

----------------------------------------------------------------------------------------------------------*/
[RoutineDef, isCurrentSubtoolNameSameAsFilename
,
	[VarSet, xnamesubtool, [FileNameExtract, [GetActiveToolPath], 2 ]]
	[VarSet, currentsubtoolpath, [IGetTitle, Tool:Current Tool]]

	//[VarSet, ismaintool, (([StrLength, xnamesubtool]!=[StrLength, currentsubtoolpath]) && ([StrFind, xnamesubtool, currentsubtoolpath] > 0))]
	[VarSet, ismaintool, ([StrLength, xnamesubtool] > 0 && ([StrLength, xnamesubtool]!=[StrLength, currentsubtoolpath]) && ([StrFind, xnamesubtool, currentsubtoolpath] > 0))]

	//[Note, [StrMerge,
	//	"xnamesubtool: ", xnamesubtool, "\n\n",
	//	"currentsubtoolpath: ", currentsubtoolpath, "\n\n",
	//	"ismaintool: ", ismaintool
	//]]
,
	/* parameters */
	ismaintool
]


/*--------------------------------------------------------------------------------------------------------

	SEARCH FOR SUBTOOL NAMED AS AS FILE IN PATH

	IF PATH NOT EXIST THEN: TRY GET LAST USED PATH
	

	1) Main subtool can be MOVED
	2) Main subtool can be RENAMED

	3) FileNameGetLastTyped must be path to ".ztl"
	4) Subtool matching ".ztl" file must exists

	@return fiepath and subtool index

----------------------------------------------------------------------------------------------------------*/

[RoutineDef, searchForSubtoolByPath
,

	/* GET LAST USED PATH */ 
	[If, [StrLength, path ] == 0
	, // THEN
		[VarSet, path, [FileNameGetLastTyped]]
	]
	
	
	[VarSet, namelastused, [FileNameExtract, path, 2]]
	[VarSet, extlastused,  [FileNameExtract, path, 4]]

	[VarSet, ztlext, ".ztl"]

	[VarSet, lastfileisztl, (([StrLength, extlastused]==[StrLength, ztlext]) && ([StrFind, extlastused, ztlext] > -1))]


	/* IF PATH TO FILE IS ".ztl" */
	[If, (([StrLength, extlastused]==[StrLength, ztlext]) && ([StrFind, extlastused, ztlext] > -1)) == 1
	//[If, [Var, lastfileisztl]
	, // THEN

		[SubToolSelect, 0]

		/* SEARCH FOR SUBTOOOL BY FILENAME */
		[Loop, [SubToolGetCount],

			[If,  ( #index < 0 )
			, // THEN
				[SubToolSelect, i ]

				/* COMPARE FILENAME AND SUBTOOL NAME */
				[VarSet, xnamesubtool, [FileNameExtract, [GetActiveToolPath], 2 ]]

				[VarSet, foundsubtool, (([StrLength, xnamesubtool]==[StrLength, namelastused]) && ([StrFind, xnamesubtool, namelastused] > -1))]

				/* GET SUBTOOL INDEX IF FOUND  */
				[If,  ( [Var, foundsubtool] > 0 ),
					[VarSet, index, i ]]
			]

		, i] // end loop
	]
, /* parameters and output */
	path, index
]
